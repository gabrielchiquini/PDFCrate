plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm' version '1.7.20'
    id "org.jetbrains.dokka" version "1.7.20"

    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'

    id 'maven-publish'
}

java {
    withSourcesJar()
}

task javadocJar(type: Jar) {
    dependsOn(dokkaJavadoc)
    archiveClassifier.set("javadoc")
    from(dokkaJavadoc.outputDirectory)
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact javadocJar

            groupId = 'pdfcrate'
            artifactId = 'pdfcrate'
            version = '0.1.0'

            from components.java

            pom {
                name = 'PDFCrate'
                description = 'A component-based library to generate PDFs built on top of PDFBox'
                url = 'https://github.com/gabrielchiquini/PDFCrate'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:gabrielchiquini/PDFCrate.git'
                    developerConnection = 'scm:git:git@github.com:gabrielchiquini/PDFCrate.git'
                    url = 'https://github.com/gabrielchiquini/PDFCrate'
                }
            }
        }
    }
}

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
}

dependencies {
    dokkaJavadocPlugin("org.jetbrains.dokka:kotlin-as-java-plugin:1.7.20")
    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin test library.
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // Use the Kotlin JUnit integration.
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'
    // https://mvnrepository.com/artifact/org.apache.pdfbox/pdfbox
    implementation 'org.apache.pdfbox:pdfbox:2.0.26'
    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
    testImplementation "org.assertj:assertj-core:3.23.1"
    // https://mvnrepository.com/artifact/io.mockk/mockk
    testImplementation 'io.mockk:mockk:1.13.2'

}

task customFatJar(type: Jar) {
    archiveName = 'pdfcrate-bundle.jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}
